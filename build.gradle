// **********************************************************************
//
// Copyright (c) 2003-present ZeroC, Inc. All rights reserved.
//
// **********************************************************************

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

def pluginVersion = "4.0.1"

jar {
    manifest {
        attributes("Built-By": "ZeroC, Inc.")
    }
}

sourceSets {
    main {
        java {
            srcDir "src"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.ant', name: 'ant', version: '1.10.5'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        antIce(MavenPublication) {
            groupId = 'com.zeroc'
            artifactId = 'ant-ice'
            version = pluginVersion
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Ice Builder for Ant'
                description = 'Ant tasks to automate the compilation of Slice files to Java'
                url = 'https://zeroc.com'

                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://github.com/zeroc-ice/ice-builder-ant/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.antIce
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
